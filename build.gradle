apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = 'live.radix'
version = '0.1.2-SNAPSHOT'

repositories {
    mavenCentral()
}


buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE'
    }
}

allprojects {

    repositories {
        mavenLocal()
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    tasks.withType(Test) {
        systemProperty "file.encoding", "UTF-8"
    }

    tasks.withType(Javadoc) {
        failOnError false
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

ext {
    jackson_version = "2.12.6"
    jackson_databind_version = '2.12.6.1'
    jackson_databind_nullable_version = "0.2.1"
}

subprojects {
    apply plugin: "maven-publish"

    project.afterEvaluate {
        java {
            withSourcesJar()
            withJavadocJar()
        }

        publishing {
            repositories {
                mavenLocal()
                maven {
                    name = 'OSSRH'
                    def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                    def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username = ossrhUsername
                        password = ossrhPassword
                    }
                }
            }

            publications {
                main(MavenPublication) {
                    from components.java

                    pom {
                        name = project.name
                        packaging = 'jar'
                        url = 'https://github.com/Radix-Live/radix-java-common'

                        scm {
                            connection = 'scm:git:https://github.com/Radix-Live/radix-java-common.git'
                            developerConnection = 'scm:git:https://github.com/Radix-Live/radix-java-common.git'
                            url = 'https://github.com/Radix-Live/radix-java-common'
                        }

                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'https://opensource.org/licenses/mit-license.php'
                            }
                        }

                        developers {
                            developer {
                                id = 'Mleekko'
                                name = 'Oleh Koval'
                                email = 'oleh@koval.tech'
                            }
                        }
                    }
                }
            }
        }

        signing {
            required { gradle.taskGraph.hasTask("publish") && hasProperty("signing.keyId") }
            sign publishing.publications.main
        }
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        overriddenByDependencies = false

        dependencies {
            dependency('com.google.inject:guice:5.0.1') {
                exclude 'com.google.guava:guava'
            }

            dependency 'org.apache.logging.log4j:log4j-api:2.17.2'
            dependency 'org.apache.logging.log4j:log4j-core:2.17.2'
            dependency 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.2'

            dependency('org.reflections:reflections:0.9.12') {
                exclude 'com.google.guava:guava'
            }

            dependency 'org.bouncycastle:bcprov-jdk18on:1.71'
            dependency 'org.bouncycastle:bcpkix-jdk18on:1.71'

            dependency 'org.json:json:20180813'

            dependency "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
            dependency "com.fasterxml.jackson.core:jackson-core:$jackson_version"
            dependency "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"
            dependency "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:$jackson_version"
            dependency("com.fasterxml.jackson.datatype:jackson-datatype-json-org:$jackson_version") {
                exclude 'org.json:json'
            }
            dependency("com.fasterxml.jackson.datatype:jackson-datatype-guava:$jackson_version") {
                exclude 'com.google.guava:guava'
            }

            dependency 'com.google.guava:guava:31.1-jre'

            // need at least this commit (or later): https://github.com/bitcoinj/bitcoinj/commit/7629677103dbdd80f14c63b066ed27e1fad47014
            // in order to work with BouncyCastle 1.64 or later
            // see issue: https://github.com/bitcoinj/bitcoinj/issues/1951
            dependency('org.bitcoinj:bitcoinj-core:0.16.1') {
                exclude 'com.google.guava:guava'
                exclude 'org.bouncycastle:bcprov-jdk15to18'
            }

            dependency 'org.mockito:mockito-core:3.12.4'
            dependency 'nl.jqno.equalsverifier:equalsverifier:3.6.1'
            dependency 'org.assertj:assertj-core:3.20.2'
            dependency 'junit:junit:4.13.2'
        }
    }

}
