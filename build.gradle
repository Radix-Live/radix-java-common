apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'


group = 'live.radix'
version = '0.3.0-SNAPSHOT'

repositories {
    mavenCentral()
}


buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.15.RELEASE'
    }
}

allprojects {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenLocal()
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    tasks.withType(Test) {
        systemProperty "file.encoding", "UTF-8"
    }

    tasks.withType(Javadoc) {
        failOnError false
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

ext {
    jackson_version = '2.13.3'
    jackson_databind_version = '2.13.3'
    junit_version = '4.13.2'
}

subprojects {
    apply plugin: "maven-publish"

    ext {
        license = {
            name = 'MIT License'
            url = 'https://opensource.org/licenses/mit-license.php'
        }
    }

    afterEvaluate {
        java {
            withSourcesJar()
            withJavadocJar()
        }

        publishing {
            repositories {
                mavenLocal()
                maven {
                    name = 'OSSRH'
                    def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                    def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username = ossrhUsername
                        password = ossrhPassword
                    }
                }
            }
            String publicationName = project.name
            publications {
                "$publicationName"(MavenPublication) {
                    from components.java

                    pom {
                        name = project.name
                        description = project.description
                        packaging = 'jar'
                        url = 'https://github.com/Radix-Live/radix-java-common'

                        scm {
                            connection = 'scm:git:https://github.com/Radix-Live/radix-java-common.git'
                            developerConnection = 'scm:git:https://github.com/Radix-Live/radix-java-common.git'
                            url = 'https://github.com/Radix-Live/radix-java-common'
                        }

                        licenses {
                            license license
                        }

                        developers {
                            developer {
                                id = 'Mleekko'
                                name = 'Oleh Koval'
                                email = 'oleh@koval.tech'
                            }
                        }
                    }
                }
            }

            signing {
                required { gradle.taskGraph.hasTask("publish") && hasProperty("signing.keyId") }
                sign publishing.publications."$publicationName"
            }
        }

    }

}
